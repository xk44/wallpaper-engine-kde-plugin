import QtQuick 2.5
import com.github.catsout.wallpaperEngineKde 1.2
import ".."

Item{
    id: videoItem
    anchors.fill: parent
    property alias source: player.source
    readonly property int displayMode: background.displayMode
    readonly property real videoRate: background.speed
    readonly property bool stats: background.mpvStats
    property var volumeFade: Common.createVolumeFade(
        videoItem, 
        Qt.binding(function() { return background.mute ? 0 : background.volume; }),
        (volume) => { player.volume = volume; }
    )
    
    onDisplayModeChanged: {
        if(videoItem.displayMode == Common.DisplayMode.Crop) {
            player.setProperty("keepaspect", true);
            player.setProperty("panscan", 1.0);
        } else if(videoItem.displayMode == Common.DisplayMode.Aspect) {
            player.setProperty("keepaspect", true);
            player.setProperty("panscan", 0.0);
        } else if(videoItem.displayMode == Common.DisplayMode.Scale) {
            player.setProperty("keepaspect", false);
            player.setProperty("panscan", 0.0);
        }
    }

    // Force displayMode update on background.displayMode change
    Timer {
        id: displayModeFixTimer
        interval: 50
        repeat: false
        onTriggered: videoItem.displayModeChanged()
    }
    Connections {
        target: background
        function onDisplayModeChanged() {
            displayModeFixTimer.restart();
        }
    }
    // it's ok for toggle, true will always cause a signal at first
    onStatsChanged: {
        player.command(["script-binding","stats/display-stats-toggle"]);
    }

    onVideoRateChanged: player.setProperty('speed', videoRate);

    // logfile
    // source
    // mute
    // volume
    // fun:setProperty(name,value)
    Mpv {
        id: player
        anchors.fill: parent
        mute: background.mute
        volume: 0
        hwdec: background.mpvHwdec
        fpsLimit: background.mpvFpsLimit
        renderScale: background.mpvRenderScale
        Connections {
            ignoreUnknownSignals: true
            onFirstFrame: {
                background.sig_backendFirstFrame('mpv');
            }
        }
    }

    // Debug HUD — shown when mpvStats is enabled
    Rectangle {
        id: debugHud
        visible: videoItem.stats
        anchors.top: parent.top
        anchors.left: parent.left
        anchors.margins: 8
        width: debugText.implicitWidth + 16
        height: debugText.implicitHeight + 12
        color: "#CC000000"
        radius: 4

        property int tick: 0

        function formatMetrics() {
            var m = player.debugMetrics();
            var lines = "path: " + m.renderPath
                + "\nscale: " + m.renderScale.toFixed(2)
                + "\nframes: " + m.frameCount
                + "  dropped: " + m.droppedFrames
                + "\nrender: " + m.lastRenderMs.toFixed(2) + " ms";
            if (m.renderPath === "sw-fallback") {
                lines += "\nalpha: " + m.lastAlphaFixMs.toFixed(2)
                    + " ms  upload: " + m.lastUploadMs.toFixed(2) + " ms";
            }
            return lines;
        }

        Text {
            id: debugText
            anchors.centerIn: parent
            color: "#00FF00"
            font.family: "monospace"
            font.pixelSize: 12
            text: debugHud.tick >= 0 ? debugHud.formatMetrics() : ""
        }

        Timer {
            running: debugHud.visible
            interval: 500
            repeat: true
            onTriggered: debugHud.tick++
        }
    }
    Component.onCompleted:{
        background.nowBackend = 'mpv';
        videoItem.displayModeChanged();
    }

    function play(){
        // stop pause time to avoid quick switch which cause keep pause 
        pauseTimer.stop();
        player.play();
        volumeFade.start();
    }
    function pause(){
        volumeFade.stop();
        pauseTimer.start();
    }
    Timer{
        id: pauseTimer
        running: false
        repeat: false
        interval: 200
        onTriggered: {
            player.pause();
        }
    }
    function getMouseTarget() {
    }
}
